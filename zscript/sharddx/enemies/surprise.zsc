/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_BossSpot : SpecialSpot
{
	Default
	{
		//$Category "Monsters"
		//$Sprite internal:action
		//$Title "Boss Teleport Spot"
		
		+Invisible
	}
}

Class SHDX_SurpriseBoss : SHDX_Boss
{
	Default
	{
		//$Category "Monsters"
		//$Sprite SURPA1
		//$Title "Boss 1 - Ms. Surprise"
		
		Health 1400;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 0;
		
		PainChance "Explosive", 256;
		PainChance "Bullet", 128;
		PainChance "Lightning", 128;
		PainChance "Spectrum", 256;
		PainChance "Ice", 64;
		DamageFactor "Bullet", 0.50;
		DamageFactor "Explosive", 0.75;
		DamageFactor "Spectrum", 0.10;
		DamageFactor "Ice", 0.35;
		// SeeSound "boss1/sight";
		PainSound "boss1/pain";
		DeathSound "boss1/death";
		// ActiveSound "boss1/active";
		Obituary "$SHDX_OB_SURPRISE";
		Tag "$SHDX_TAG_SURPRISE";
	
		+DONTRIP
		+NOICEDEATH
		
		
		+SHDX_Boss.TriggerLevelEnd
		+SHDX_Boss.TriggerCutscene
		
		SHDX_Boss.CutsceneToPlay 'EndEpisode1';
		
		SHDX_Boss.TauntCooldownTime 10;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		SeeSound = "boss1/sight"..Random[Surprise](1,4);
	}
	
	void A_SurpriseSetBossMusic()
	{
		S_ChangeMusic("M_SURP");
	}
	
	void A_SurpriseSetBossVictoryMusic()
	{
		S_ChangeMusic("M_SURPV", 0, false);
	}
	
	void A_SurpriseTaunt()	
	{
		A_SHDX_PlayBossTaunt("boss1/taunt"..Random[Surprise](1,6));
	}
	
	override void MarkPreCacheSounds()
	{
		Super.MarkPreCacheSounds();
		
		Sound snd;
		for (int i = 1; i < 7; i++)
		{
			snd = String.Format("boss1/taunt%d", i);
			MarkSound(snd);
		}
	}
	
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		int ret = Super.DamageMobj(inflictor, source, damage, mod, flags, angle);
		
		if (mod == 'Massacre')
			return ret;
		
		SurpriseCheckForLowerFloor();
		
		return ret;
	}
	
	States
	{
		Spawn:
			SURP A 0 NoDelay
			{
				isactive = false;
			}
		Spawn1:
			SURP AA 10
			{
				A_RemoveLight('surpriselight');
				A_SetInvulnerable();
				A_Look();
			}
			Loop;
		See:
			SURP A 0
			{
				A_StartSound(SeeSound, CHAN_VOICE, attenuation:ATTN_NONE);
				A_SurpriseSetBossMusic();
				bCanJump = false;
			}
			SURP A 75 A_FaceTarget();
		See1:
			SURP A 0
			{
				A_SHDX_SendOLToAllPlayers("scootaloo/surprisestart", OLSSF_FORCEONELINER|OLSSF_BYPASSMUTE|OLSSF_BYPASSPLAYERPREFIX|OLSSF_NOGRIN);
				A_SHDX_SetToBossBarHUD();
				A_SurpriseSetBossMusic();
				isactive = true;
			}
		See2:
			SURP A 0 A_UnsetInvulnerable;
			SURP A 20
			{
				A_Chase(null, null, CHF_DONTMOVE);
				A_FaceTarget();
			}
		TeleportAboutInitial:
			SURP D 2 A_SetTranslucent(0.5, 0);
			SURP D 1;
			SURP D 0 A_Teleport("JustTeleportedInitial", "SHDX_BossSpot", "SHDX_TeleportFog", 0);
			SURP D 1;
			Goto See3;
		JustTeleportedInitial:
			SURP D 0 A_UnSetInvulnerable();
			SURP D 0 A_AttachLightDef('surpriselight', 'SHDX_BOSSLIGHT');
			SURP D 3 A_SetTranslucent(1.0, 0);
			SURP D 3 A_StartSound("boss1/active", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
			goto Missile;
		See3:
			SURP A 20
			{
				A_Chase(null, null, CHF_DONTMOVE);
				A_FaceTarget();
			}
			SURP A 0 A_Jump(96, "TeleportAbout");
			Loop;
			
		Missile:
			SURP B 3
			{
				A_FaceTarget();
				A_SurpriseTaunt();
			}
			SURP C 5
			{
				A_StartSound("boss1/stomp", CHAN_WEAPON);
				A_SpawnProjectile("SHDX_SurpriseTornado");
			}
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			goto TeleportAbout;
		JustTeleported:
			SURP D 0 A_SpawnItemEx("SHDX_TeleportFog", flags:SXF_NOCHECKPOSITION);
			SURP D 3 A_SetTranslucent(1.0, 0);
			SURP D 3 A_StartSound("boss1/active", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
			SURP D 3 A_Jump(128, "TeleportAbout");
			goto Missile;
		TeleportAbout:
			SURP D 2 A_SetTranslucent(0.5, 0);
			SURP D 1;
			SURP D 1 A_Teleport("JustTeleported", "SHDX_BossSpot", "SHDX_NoFlash", 0);
			Goto See3;
		Pain.Bullet:
		Pain.Explosive:
		Pain.Spectrum:
		Pain.Ice:
			SURP A 0
			{
			}
			SURP H 5
			{
				A_StartSound("boss1/pain"..Random[Surprise](1,5), CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			SURP H 5
			{
				SetShade("f08010");
			}
			SURP H 5
			{
				SetShade("ffee6a");
			}
			SURP H 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			Goto TeleportAbout;
			
		SeeFloorPeelaway:
			SURP H 5
			{
				bNoPain = true;
				A_SetInvulnerable();
				A_StartSound("boss1/pain5", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			SURP H 5
			{
				SetShade("f08010");
			}
			SURP H 5
			{
				SetShade("ffee6a");
			}
			SURP H 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
				A_StartSound("boss1/surprisefloorpeel", CHAN_VOICE, attenuation:ATTN_NONE);
				A_SHDX_BossSubtitle("$SHDX_VOX_SURPRISE_DROP", "boss1/surprisefloorpeel");
				bCanJump = false;
			}
			SURP H 180;
			SURP H 0
			{
				A_SurpriseLowerFloor();
				A_UnSetInvulnerable();
				bCanJump = true;
				bNoPain = false;
			}
			Goto TeleportAbout;
			
		Death.Explosive:
		Death.Weather:
		Death.Spectrum:
		XDeath:
		Death:
			SURP H 105
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				A_RemoveLight('surpriselight');
				SetShade("ffffff");
				A_SurpriseSetBossVictoryMusic();
			}
			SURP I 5 A_NoBlocking;
			SURP J 5
			{
				A_SpawnGibsFx(SGFX_SPAWNNOEXTRA);
				A_NewBossScream();
				A_SHDX_BossSubtitle("$SHDX_VOX_SURPRISE_DEATH", DeathSound);
			}
			SURP KLM 5;
			SURP M 100;
			SURP M 125 A_SHDX_SendOLToAllPlayers("scootaloo/surpriseend", OLSSF_FORCEONELINER|OLSSF_BYPASSMUTE|OLSSF_BYPASSPLAYERPREFIX|OLSSF_NOGRIN);
			SURP M 1 A_DD2_BossPlayCutscene();
			SURP M -1 A_DD2_BossEndLevel();
			Stop;
	}
}

Class SHDX_SurpriseTornado : SHDX_Projectile
{
	Default
	{
		Radius 16;
		Height 56;
		Speed 8;
		Damage 10;
		DamageType "Weather";
		+MISSILE
		+DROPOFF
		+RIPPER
		+NOEXPLODEFLOOR
		+STEPMISSILE
		MaxStepHeight 16;
		Projectile;
		+SEEKERMISSILE
	}
	States
	{
		Spawn:
			TORN A 0
			{
				A_SeekerMissile(1, 15);
				A_StartSound("boss1/tornado", CHAN_BODY);
			}
			TORN ABCD 1;
			Loop;
		Death:
			TORN A 3
			{
				A_FadeTo(0.0, 0.1, true);
			}
			TORN BCD 3;
			loop;
	}
	
	override void Tick()
	{
		Super.Tick();
		if (target.health <= 0 && !InStateSequence(CurState, ResolveState("Death")))
			SetStateLabel("Death");
	}
	
	override int DoSpecialDamage(Actor thingtodmg, int damage, Name damagetype)
	{
		if (level.maptime & 16)
		{
			thingtodmg.angle += RandomPick[Tornado](90, -45, -22, 0, 22, 45, 90);
			thingtodmg.vel.z = Random[Tornado](4, 12);
		}
		
		if (level.maptime & 4)
		{
			thingtodmg.DamageMobj(self.target, self.target, 15, 'Weather');
		}
		
		return -1;
	}
}
