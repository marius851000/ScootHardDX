/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_PegapolBullet : FastProjectile
{
	Default
	{
		Speed 120;
		Radius 8;
		Height 8;
		DamageFunction Random[Pegapol](4,8);
		DamageType "Bullet";
		Scale 0.12;
		MissileType "SHDX_BulletTrail";
		MissileHeight 8;
		RenderStyle "Translucent";
		Alpha 0.667;
		
		+CANNOTPUSH
		+NODAMAGETHRUST
		+SPAWNSOUNDSOURCE
		//+ALLOWTHRUFLAGS
		//+THRUSPECIES
		//+MTHRUSPECIES
		+BloodSplatter
	}
	States
	{
		Spawn:
			BULL A 4 Bright;
			Loop;
		Death:
			PUFF ABCD 4 Bright;
			Stop;
	}
}

Class SHDX_PegapolMale : SHDX_Enemy
{
	Default
	{
		//$Category "Monsters"
		//$Sprite PEGAA1
		//$Title "Pegapol (Male)"
		Health 100;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 128;
		MeleeRange 8;
		Damage 5;
		DamageFactor "Bullet", 0.95;
		DamageFactor "Explosive", 0.85;
		DamageFactor "Lightning", 0.85;
		DamageFactor "Ice", 0.50;
		// DamageFactor "Rainbower", 0.25;
		// DamageFactor "RainbowerX", 0.25;
		SeeSound "pegapol/sight";
		PainSound "pegapol/pain";
		DeathSound "pegapol/death";
		// ActiveSound "pegapol/active";
		Obituary "$SHDX_OB_PEGAPOL";
		// DropItem "SHBitCoinPickupGold" 127
		// DropItem "SHSubmachineGun" 32
		Tag "$SHDX_TAG_PEGAPOL";
		Species "Pony";
	}
	
	int flyingmode;
	
	void A_DropItemsPegapol()
	{
		if (target && target.CountInv("SHDX_BitCoin") < 50)
		{
			A_SHDX_DropItem("SHDX_SilverBitCoin", false);
		}
		
		if (target && target.CountInv("SHDX_SubmachineGun") <= 0)
			A_SHDX_DropItem("SHDX_SubmachineGun");
	}
	
	void A_PegapolFire()
	{
		A_StartSound("pegapol/attack", CHAN_WEAPON);
		A_SpawnProjectile("SHDX_PegapolBullet", 16);
	}
	
	void A_FlapWings()
	{
		A_StartSound("pegapol/wings", CHAN_6);
	}
	
	States
	{
		Spawn:
			PEGA A 10 A_Look;
			Loop;
		See:
			PEGA B 0
			{
				if (flyingmode >= 1)
					return ResolveState("See.Flying");
				
				return ResolveState(null);
			}
			PEGA BCDEBCDE 4 A_Chase;
			PEGA E 0
			{
				if (random[Pegapol](0, 100) >= 72)
					return ResolveState("StartFlying");
				
				return ResolveState(null);
			}
			Loop;
		See.Flying:
			PEGA O 4
			{
				A_Chase();
				A_FlapWings();
			}
			PEGA PQR 4
			{
				A_Chase();
			}
			PEGA O 4
			{
				A_Chase();
				A_FlapWings();
			}
			PEGA PQR 4
			{
				A_Chase();
			}
			PEGA R 0
			{
				flyingmode++;
				if (flyingmode >= 15)
					return ResolveState("StopFlying");
					
				return ResolveState(null);
			}
			Loop;
		StartFlying:
			PEGA O 0
			{
				bNoGravity = true;
				bFloat = true;
				flyingmode = 1;
			}
			Goto See.Flying;
		Missile:
			PEGA F 0
			{
				if (flyingmode >= 1)
					return ResolveState("Missile.Flying");
				
				return ResolveState(null);
			}
			PEGA F 5 A_FaceTarget;
			PEGA GH 2 Bright A_PegapolFire;
			PEGA IF 2 A_FaceTarget;
			PEGA GH 2 Bright A_PegapolFire;
			PEGA IF 2 A_FaceTarget;
			PEGA GH 2 Bright A_PegapolFire;
			PEGA IF 2 A_FaceTarget;
			Goto See;
		Missile.Flying:
			PEGA O 0
			{
				A_FaceTarget();
				A_StartSound("pegapol/charge", CHAN_7);
				A_FlapWings();
			}
			PEGA OPQR 5
			{
				A_SkullAttack(15);
			}
			PEGA OPQR 5
			{
				A_SkullAttack(15);
			}
			PEGA OPQR 5
			{
				A_SkullAttack(15);
			}
			Goto Missile.StopFlying;
		Missile.StopFlying:
			PEGA R 0
			{
				bNoGravity = false;
				bFloat = false;
				flyingmode = 0;
			}
			Goto Missile+1;
		Pain:
			PEGA J 0
			{
				if (flyingmode >= 1)
				{
					if (Random[Pegapol](0, 1))
						return ResolveState("Pain.Flying1");
					
					return ResolveState("Pain.Flying2");
				}
				
				if (Random[Pegapol](0, 1))
					return ResolveState("Pain1");
				
				return ResolveState("Pain2");
			}
		Pain1:
			PEGA S 3
			{
				VelFromAngle(speed, angle+90);
			}
			PEGA S 3 A_Pain;
			Goto Missile;
		Pain2:
			PEGA T 3
			{
				VelFromAngle(speed, angle-90);
			}
			PEGA T 3 A_Pain;
			Goto Missile;
		Pain.Flying1:
			PEGA S 3 A_Pain;
			Goto StopFlying;
		Pain.Flying2:
			PEGA T 3 A_Pain;
			Goto StopFlying;
		StopFlying:
			PEGA S 0
			{
				flyingmode = 0;
				bNoGravity = false;
				bFloat = false;
			}
			Goto See;
		Death:
			PEGA K 5 A_Scream;
			PEGA L 5
			{
				A_NoBlocking();
				A_DropItemsPegapol();
			}
			PEGA M 5;
			PEGA N -1 A_SetCorpseShootable;
			Stop;
		Ice:
			PEGA S 5
			{
				A_GenericFreezeDeath();
			}
			PEGA S 5
			{
				A_FreezeDeathChunks();
			}
			Wait;
		Death.Explosive:
		Death.Weather:
		Death.Extreme:
		XDeath:
			GIBX A 3
			{
				A_SHDX_ExtremeScreamAndUnblock();
				A_DropItemsPegapol();
				A_SpawnGibsFx(self);
			}
			GIBX B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBX BC 5;
			GIBX D -1;
			Stop;
		Death.RainbowerX:
			GIBR A 4;
			GIBR B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBR BC 4;
			GIBR D -1;
			Stop;
		Crush:
			GIBX D 0 A_SHDX_SquishSound;
			GIBX D 1 A_SHDX_GivePlayerOneLiner;
			GIBX D -1;
			Stop;
	}
}