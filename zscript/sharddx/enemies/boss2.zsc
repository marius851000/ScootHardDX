/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_AtmosphereTornado : SHDX_SurpriseTornado
{
	Default
	{
		Radius 16;
		Height 56;
		Speed 10;
		Damage 10;
	}
	
	States
	{
		Spawn:
			TORN A 0
			{
				A_SeekerMissile(1, 24);
				A_StartSound("boss2/tornado", CHAN_BODY);
			}
			TORN ABCD 1;
			Loop;
		Death:
			TORN A 0
			{
				A_FadeTo(0.0, 0.1, true);
				A_SetScale((scale.X-0.1), scale.Y);
			}
			TORN ABCD 3;
			loop;
	}
}

Class SHDX_FloorArc : SHDX_Projectile
{
	Default
	{
		Radius 16;
		Height 56;
		Speed 8;
		Damage 6;
		+MISSILE
		+DROPOFF
		+NOEXPLODEFLOOR
		+STEPMISSILE
		MaxStepHeight 16;
		Projectile;
		+SEEKERMISSILE
		+FLOORHUGGER
		
		DamageType "Lightning";
	}
	
	override void Tick()
	{
		Super.Tick();
		if (target.health <= 0 && !InStateSequence(CurState, ResolveState("Death")))
			Destroy();
	}
	
	States
	{
		Spawn:
			ATLX A 0
			{
				A_SeekerMissile(1, 15);
				A_StartSound("misc/arc", CHAN_BODY);
			}
			ATLX ABAB 1;
			Loop;
		Death:
			ATLX A 0
			{
				A_FadeTo(0.0, 0.1, true);
				A_SetScale((scale.X-0.1), scale.Y);
			}
			ATLX AB 1;
			loop;
	}
}

Class SHDX_DrAtmosphereBoss : SHDX_Boss
{
	Default
	{
		//$Category "Monsters"
		//$Sprite ATMOA2
		//$Title "Boss 2 - Dr Atmosphere"
		
		Health 3000;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 12;
		PainChance "Explosive", 256;
		PainChance "Bullet", 128;
		PainChance "Lightning", 128;
		PainChance "Spectrum", 256;
		PainChance "Ice", 64;
		DamageFactor "Bullet", 0.50;
		DamageFactor "Explosive", 0.75;
		DamageFactor "Chicken", 0.00;
		DamageFactor "ChickenX", 0.00;
		DamageFactor "Spectrum", 0.10;
		DamageFactor "Ice", 0.35;
		// SeeSound "boss2/sight";
		PainSound "boss2/pain";
		DeathSound "boss2/death";
		Obituary "$SHDX_OB_DRATMOS";
		Tag "$SHDX_TAG_DRATMOS";
		
		+DONTRIP
		+NOICEDEATH
		
		+SHDX_Boss.TriggerLevelEnd
		+SHDX_Boss.TriggerCutscene
		
		SHDX_Boss.CutsceneToPlay 'EndEpisode2';
	}
	
	bool fogchanged;
	
	override void Tick()
	{
		Super.Tick();
		if (flyingmode > 0)
			isflying = true;
		else
			isflying = false;
	}
	
	const ATMOSFLY = 20;
	
	void A_FlyTowardsTarget(double speedtofly = ATMOSFLY)
	{
		if (!target) return;
		
		if (speedtofly <= 0) speedtofly = ATMOSFLY;
		
		A_FaceTarget();
		
		VelFromAngle(speedtofly);
		Vel.Z = (target.pos.Z + target.Height/2 - pos.Z) / DistanceBySpeed(target, speedtofly);
	}
	
	void A_AtmosphereSetBossMusic()
	{
		S_ChangeMusic("M_ATMO");
	}
	
	void A_AtmosphereTaunt()
	{
		A_SHDX_PlayBossTaunt("boss2/taunt"..Random[DrAtmosphere](1,11));
	}
	
	override void MarkPreCacheSounds()
	{
		Super.MarkPreCacheSounds();
		Sound snd;
		for (int i = 1; i < 12; i++)
		{
			snd = String.Format("boss2/taunt%d", i);
			MarkSound(snd);
		}
	}
	
	void A_FlapWings()
	{
		A_StartSound("boss2/wings", CHAN_6);
	}
	
	void A_AtmosphereVictoryMusic()
	{
		S_ChangeMusic("M_ATVIC", 0, false);
	}
	
	void A_FogDensityMachine()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(152);
			s.SetFade("6f1306");
			TextureID skyid1 = TexMan.CheckForTexture("SKY3C", TexMan.Type_Any);
			TextureID skyid2 = TexMan.CheckForTexture("SKY3A", TexMan.Type_Any);
			level.ChangeSky(skyid1, skyid2);
		}
		fogchanged = true;
	}
	
	void A_FogScaleUp1()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(164);
			s.SetFade("661011");
		}
	}
	
	void A_FogScaleUp2()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(172);
			s.SetFade("610e16");
		}
	}
	
	void A_FogScaleUp3()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(178);
			s.SetFade("580b21");
		}
	}
	
	void A_FogScaleUp4()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(182);
			s.SetFade("540a27");
		}
	}
	
	void A_FogScaleUp5()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(192);
			s.SetFade("4b0731");
		}
	}
	
	void A_FogScaleUp6()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(194);
			s.SetFade("4b0731");
		}
	}
	
	void A_FogScaleUp7()
	{
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetFogDensity(200);
			s.SetFade("390147");
		}
	}
	
	States
	{
		Spawn:
			ATMO X 0 NoDelay
			{
				isactive = false;
			}
		Spawn1:
			ATMO X 10
			{
				A_RemoveLight('atmoslight');
				A_SetInvulnerable();
				A_Look();
			}
			Loop;
		See:
			ATMO X 0
			{
				A_AtmosphereSetBossMusic();
			}
			ATMO X 0 A_SHDX_BossJumpOverIntroState;
			ATMO X 0
			{
				A_SHDX_PlayBossTaunt("boss2/sight", true);
				A_SHDX_BossSubtitle("$SHDX_VOX_DRATMOS_9", "boss2/sight");
				bCanJump = false;
			}
			ATMO X 425;
		See1:
			ATMO B 0
			{
				A_SHDX_SendOLToAllPlayers("scootaloo/atmospherestart", OLSSF_FORCEONELINER|OLSSF_BYPASSMUTE|OLSSF_BYPASSPLAYERPREFIX|OLSSF_NOGRIN);
				
				A_AtmosphereSetBossMusic()
				;
				A_SHDX_BossSkipIntro();
			}
		See1A:
			ATMO B 0 A_SHDX_BossInit;
		See2:
			ATMO B 0
			{
				if (flyingmode >= 1)
					return ResolveState("See2.Flying");
				
				return ResolveState(null);
			}
			// ATMO B 0 A_StartSound("pony/walk2", CHAN_BODY);
			ATMO BCDE 2 A_Chase;
			// ATMO B 0 A_StartSound("pony/walk2", CHAN_BODY);
			ATMO BCDE 2 A_Chase;
			ATMO E 0 A_Jump(32, "StartFlying");
			Loop;
		See2.Flying:
			ATMO F 4
			{
				A_Chase(null, "Missile.Flying");
				A_FlapWings();
			}
			ATMO GHI 4
			{
				A_Chase(null, "Missile.Flying");
			}
			ATMO F 4
			{
				A_Chase(null, "Missile.Flying");
				A_FlapWings();
			}
			ATMO GHI 4
			{
				A_Chase(null, "Missile.Flying");
			}
			ATMO I 0
			{
				flyingmode++;
				if (flyingmode >= 15)
					return ResolveState("StopFlying");
					
				return ResolveState(null);
			}
			Loop;
		StartFlying:
			ATMO F 0
			{
				A_StopSound(CHAN_BODY);
				bNoGravity = true;
				bFloat = true;
				bCanJump = false;
				flyingmode = 1;
			}
			Goto See2.Flying;
		Missile:
			ATMO M 0 A_Jump(256, "Missile.Stomp", "Missile.TriIce", "Missile.Ice");
		Missile.Ice:
			ATMO N 0 A_AtmosphereTaunt;
			ATMO N 5 A_FaceTarget;
			ATMO OP 2 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO N 2;
			ATMO OP 2 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO N 2;
			ATMO OP 2 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO N 2;
			goto See2;
		Missile.TriIce:
			ATMO N 0 A_AtmosphereTaunt;
			ATMO N 10 A_FaceTarget;
			ATMO O 4 A_StartSound("weapons/hailerf2", CHAN_WEAPON);
			ATMO O 4 Bright
			{
				for (int i = -32; i < 32; i += 16)
				{
					A_SpawnProjectile("SHDX_AtmosphereHailBall", angle:i);
				}
			}
			ATMO P 5 Bright;
			goto See2;
		Missile.Stomp:
			ATMO Q 4 A_AtmosphereTaunt;
			ATMO Q 5;
			ATMO Q 0 A_StartSound("boss2/stomp", CHAN_BODY);
			ATMO Q 0 A_Jump(256, "Missile.Stomp.Electric", "Missile.Stomp.Tornado");
		Missile.Stomp.Electric:
			ATMO R 8
			{
				A_SpawnProjectile("SHDX_FloorArc", spawnofs_xy: -32);
				A_SpawnProjectile("SHDX_FloorArc");
				A_SpawnProjectile("SHDX_FloorArc", spawnofs_xy: 32);
			}
			Goto See2;
		Missile.Stomp.Tornado:
			ATMO R 8
			{
				A_SpawnProjectile("SHDX_AtmosphereTornado");
			}
			Goto See2;
		Missile.Flying:
			ATMO F 0 A_AtmosphereTaunt;
			ATMO F 5
			{
				A_FaceTarget();
				A_StartSound("boss2/charge", CHAN_7);
				A_FlapWings();
			}
			ATMO J 2 Bright
			{
				A_SpawnProjectile("SHDX_AtmosphereHailBall");
				A_FlyTowardsTarget(15);
			}
			ATMO KL 2
			{
				A_FlyTowardsTarget(15);
			}
			ATMO M 2 Bright
			{
				A_SpawnProjectile("SHDX_AtmosphereHailBall");
				A_FlyTowardsTarget(15);
			}
			ATMO F 2;
			goto See2;
		Pain:
			SURP A 0
			{
				if (!fogchanged && health <= (SpawnHealth() / 2))
				{
					return ResolveState("SeeFogDensityMachine");
				}
				return ResolveState(null);
			}
			ATMO S 0
			{
				if (flyingmode >= 1)
				{
					return ResolveState("Pain.Flying");
				}
				
				return ResolveState(null);
			}
		Pain1:
			ATMO S 3
			{
				A_StartSound("boss2/pain"..Random[DrAtmosphere](1,3), CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			ATMO S 2
			{
				SetShade("f08010");
			}
			ATMO S 1
			{
				SetShade("ffee6a");
			}
			ATMO S 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			Goto See2;
			
		SeeFogDensityMachine:
			ATMO X 3
			{
				bNoPain = true;
				bNoGravity = false;
				bFloat = false;
				A_SetInvulnerable();
				A_StartSound("boss2/pain1", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			ATMO X 2
			{
				SetShade("f08010");
			}
			ATMO X 1
			{
				SetShade("ffee6a");
			}
			ATMO X 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
				A_StartSound("boss2/atmospherelaugh", CHAN_VOICE, attenuation:ATTN_NONE);
				A_FogDensityMachine();
				bCanJump = false;
			}
			ATMO X 5 A_FogScaleUp1();
			ATMO X 5 A_FogScaleUp2();
			ATMO X 5 A_FogScaleUp3();
			ATMO X 5 A_FogScaleUp4();
			ATMO X 5 A_FogScaleUp5();
			ATMO X 5 A_FogScaleUp6();
			ATMO X 5 A_FogScaleUp7();
			ATMO X 245;
			ATMO X 0
			{
				A_UnSetInvulnerable();
				bCanJump = true;
				bNoPain = false;
			}
			Goto See2;
		Pain.Flying:
			ATMO F 3
			{
				A_StartSound("boss2/pain"..Random[DrAtmosphere](1,3), CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			ATMO G 2
			{
				SetShade("f08010");
			}
			ATMO H 1
			{
				SetShade("ffee6a");
			}
			ATMO I 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			Goto StopFlying;
		StopFlying:
			ATMO I 0
			{
				flyingmode = 0;
				bNoGravity = false;
				bFloat = false;
				bCanJump = true;
			}
			Goto Missile;
		Death.Explosive:
		Death.Weather:
		Death.Spectrum:
		XDeath:
		Death:
			ATMO S 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
				A_RemoveLight('atmoslight');
			}
			ATMO S 0
			{
				A_AtmosphereVictoryMusic();
			}
			ATMO S 150;
			ATMO S 5;
			ATMO T 5
			{
				A_SpawnGibsFx(self);
				A_NewBossScream();
				A_SHDX_BossSubtitle("$SHDX_VOX_DRATMOS_10", DeathSound);
			}
			ATMO UV 5;
			ATMO W 55;
			ATMO W 0 A_SHDX_SendOLToAllPlayers("scootaloo/atmosphereend", OLSSF_FORCEONELINER|OLSSF_BYPASSMUTE|OLSSF_BYPASSPLAYERPREFIX|OLSSF_NOGRIN);
			ATMO W 220;
			ATMO W 1 A_DD2_BossPlayCutscene();
			ATMO W -1 A_DD2_BossEndLevel();
			Stop;
	}
}

Class SHDX_AtmosphereHailBall : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 4;
		Projectile;
		+RANDOMIZE
		DamageType "Ice";
		SeeSound "weapons/hailf";
		DeathSound "weapons/hailx";
	}
	States
	{
		Spawn:
			HAIL A 1;
			Loop;
		Death:
			HAIL B 2;
			Stop;
	}
}