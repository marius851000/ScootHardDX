/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_LowGuardBullet : FastProjectile
{
	Default
	{
		Speed 120;
		Radius 8;
		Height 8;
		DamageFunction Random[LowGuard](3,5);
		DamageType "Bullet";
		Scale 0.12;
		MissileType "SHDX_BulletTrail";
		MissileHeight 8;
		RenderStyle "Translucent";
		Alpha 0.667;
		
		+CANNOTPUSH
		+NODAMAGETHRUST
		+SPAWNSOUNDSOURCE
		//+ALLOWTHRUFLAGS
		//+THRUSPECIES
		//+MTHRUSPECIES
		+BloodSplatter
	}
	States
	{
		Spawn:
			BULL A 4 Bright;
			Loop;
		Death:
			PUFF ABCD 4 Bright;
			Stop;
	}
}

Class SHDX_LowGuardSpawner : RandomSpawner
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD1G1
		//$Title "Low Guard (Random)"
		scale 0.12;
		Radius 20;
		Height 56;
		DropItem "SHDX_LowGuardMale", 255, 7;
		DropItem "SHDX_LowGuardFemale", 255, 13;
	}
}

Class SHDX_LowGuardMale : SHDX_Enemy
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD1A1
		//$Title "Low Guard (Male)"
		SeeSound "grunt1/sight";
		PainSound "grunt1/pain";
		DeathSound "grunt1/death";
		// ActiveSound "grunt1/active";
		Health 25;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		Obituary "$SHDX_OB_LOWGUARD";
		Tag "$SHDX_TAG_LOWGUARD";
		Species "Pony";
	}
	
	void A_DropItemsLowGuard()
	{
		if (target && target.CountInv("SHDX_BitCoin") < 50)
		{
			A_SHDX_DropItem("SHDX_SilverBitCoin", false);
		}
	}
	
	void A_LowGuardFire()
	{
		A_StartSound("grunt/attack", CHAN_WEAPON);
		A_SpawnProjectile("SHDX_LowGuardBullet", 16);
	}
	
	States
	{	
		Spawn:
			GRD1 A 10 A_Look;
			loop;
		See:
			// GRD1 B 0 A_StartSound("pony/walk", CHAN_BODY);
			GRD1 BCDE 4 A_Chase;
			// GRD1 B 0 A_StartSound("pony/walk", CHAN_BODY);
			GRD1 BCDE 4 A_Chase;
			Loop;
		Missile:
			GRD1 F 0 A_StopSound(CHAN_BODY);
			GRD1 F 10 A_FaceTarget;
			GRD1 G 8 Bright A_LowGuardFire;
			GRD1 F 4;
			Goto See;
		Pain:
			GRD1 H 3;
			GRD1 H 3 A_Pain;
			Goto See;
		Pain.Lightning:
			GRD1 H 0 A_SetElectrocutionTranslation();
			GRD1 H 3;
			GRD1 H 3 A_Pain;
			GRD1 H 0 A_ResetTranslation();
			Goto See;
		Death:
			GRD1 I 0 A_ResetTranslation();
			GRD1 I 5 A_NewScream;
			GRD1 J 5
			{
				A_NoBlocking();
				A_DropItemsLowGuard();
			}
			GRD1 K 5;
			GRD1 L -1 A_SetCorpseShootable;
			Stop;
		Death.Explosive:
		Death.Weather:
		Death.Extreme:
		XDeath:
			GIBX A 0 A_ResetTranslation();
			GIBX A 3
			{
				A_SHDX_ExtremeScreamAndUnblock();
				A_DropItemsLowGuard();
				A_SpawnGibsFx(self);
			}
			GIBX B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBX BC 5;
			GIBX D -1;
			Stop;
		Death.RainbowerX:
			GIBR A 4;
			GIBR B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBR BC 4;
			GIBR D -1;
			Stop;
		Ice:
			GRD1 H 5
			{
				A_GenericFreezeDeath();
			}
			GRD1 H 1
			{
				A_FreezeDeathChunks();
			}
			Wait;
		Crush:
			GIBX D 0 A_SHDX_SquishSound;
			GIBX D 1 A_SHDX_GivePlayerOneLiner;
			GIBX D -1;
			Stop;
	}
}

Class SHDX_LowGuardFemale : SHDX_LowGuardMale
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD2A1
		//$Title "Low Guard (Female)"
		SeeSound "grunt2/sight";
		PainSound "grunt2/pain";
		DeathSound "grunt2/death";
		// ActiveSound "grunt2/active";
	}
	
	States
	{	
		Spawn:
			GRD2 A 10 A_Look;
			loop;
		See:
			// GRD2 B 0 A_StartSound("pony/walk", CHAN_BODY);
			GRD2 BCDE 4 A_Chase;
			// GRD2 B 0 A_StartSound("pony/walk", CHAN_BODY);
			GRD2 BCDE 4 A_Chase;
			Loop;
		Missile:
			GRD1 F 0 A_StopSound(CHAN_BODY);
			GRD2 F 10 A_FaceTarget;
			GRD2 G 8 Bright A_LowGuardFire;
			GRD2 F 4;
			Goto See;
		Pain:
			GRD2 H 3;
			GRD2 H 3 A_Pain;
			Goto See;
		Pain.Lightning:
			GRD2 H 0 A_SetElectrocutionTranslation();
			GRD2 H 3;
			GRD2 H 3 A_Pain;
			GRD2 H 0 A_ResetTranslation();
			Goto See;
		Ice:
			GRD2 H 5
			{
				A_GenericFreezeDeath();
			}
			GRD2 H 1
			{
				A_FreezeDeathChunks();
			}
			Wait;
		Death:
			GRD2 I 0 A_ResetTranslation();
			GRD2 I 5 A_NewScream;
			GRD2 J 5
			{
				A_NoBlocking();
				A_DropItemsLowGuard();
			}
			GRD2 K 5;
			GRD2 L -1 A_SetCorpseShootable;
			Stop;
	}
}