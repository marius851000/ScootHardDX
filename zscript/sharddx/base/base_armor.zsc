/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_Armor : Armor
{
	Default
	{
		+INVENTORY.AUTOACTIVATE;
		+INVENTORY.UNTOSSABLE;
		+INVENTORY.UNDROPPABLE;
		+INVENTORY.KEEPDEPLETED;
	}
	
	mixin PickupSoundOverride;
	
	virtual bool CanResistDamage(Name damagetype)
	{
		return false;
	}
	
	override void AbsorbDamage(int damage, Name damagetype, out int newdamage, Actor inflictor, Actor source, int flags)
	{
		bool canresist;
		bool playhitsound;
		
		if ((amount > 0) && !DamageTypeDefinition.IgnoreArmor(damagetype) && (damage > 0))
		{			
			canresist = CanResistDamage(damagetype);
			
			if (canresist)
			{
				if (amount <= damage)
				{
					newdamage = (damage - amount);
					DepleteOrDestroy();
					Destroy();	// Possible bug? This needs to be called.
					return;
				}
				else
				{
					amount -= damage;
					newdamage = 0;
					playhitsound = true;
				}
			}
			
			if (Owner && playhitsound)
				Owner.A_StartSound("armor/hit", CHAN_ARMOR);
			
			if (amount <= 0)
			{
				DepleteOrDestroy();
				Destroy();	// Possible bug? This needs to be called.
				return;
			}
		}
	}
	
	// This is basically how the ammo from GZDoom is handled
	// but for our purposes...
	
	Class<SHDX_Armor> GetParentArmor()
	{
		Class<Object> type = GetClass();
		
		while (type.GetParentClass() != "SHDX_Armor" && type.GetParentClass() != null)
		{
			type = type.GetParentClass();
		}
		
		return (Class<SHDX_Armor>)(type);
	}
	
	override Inventory CreateCopy(Actor other)
	{
		SHDX_Armor copy;
		
		int amount = Amount;
		
		Class<Object> o = GetClass();
		
		let type = GetParentArmor();
		
		if (GetClass() != type && type != null)
		{
			if (!GoAway())
				Destroy();
				
			copy = SHDX_Armor(Spawn(type));
			copy.Amount = amount;
			copy.BecomeItem();
		}
		else
		{
			copy = SHDX_Armor(Super.CreateCopy(other));
			copy.Amount = amount;
		}
		
		if (copy.Amount > copy.MaxAmount)
			copy.Amount = copy.MaxAmount;
		
		return copy;
	}
	
	override bool HandlePickup(Inventory item)
	{
		let armoritem = SHDX_Armor(item);
		
		if (armoritem != null && armoritem.GetParentClass() == GetClass())
		{
			if (Amount < MaxAmount)
			{
				int recieving = armoritem.amount;
				
				if (Amount > 0 && Amount + recieving < 0)
					Amount = 0x7fffffff;
				else
					Amount += recieving;
				
				if (Amount > MaxAmount)
					Amount = MaxAmount;
				
				item.bPickupGood = true;
			}
			
			return true;
		}
		
		return false;
	}
}
