/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

// Gib system

mixin class SHDX_GibSystem
{
	enum EGibTypeToSpawn
	{
		SGFX_SPAWNNOEXTRA = 0,
		SGFX_SPAWNHEAD = 1,
		SGFX_SPAWNEYES = 2,
		SGFX_SPAWNARM = 4,
		SGFX_SPAWNLEG = 8,
		
		SGFX_SPAWNALL = SGFX_SPAWNHEAD|SGFX_SPAWNEYES|SGFX_SPAWNARM|SGFX_SPAWNLEG,
	}
	
	// Spawn Gib and send it flying in a different direction
	void A_SHDX_SpawnGib(Class<Actor> gibtype = "SHDX_Giblets", bool dontthrust = false)
	{
		Actor a;
		
		Vector3 vel3;
		
		Vector3 pos3;
		pos3.x = self.pos.x + Random[GibFx](-16, 16);
		pos3.y = self.pos.y + Random[GibFx](-16, 16);
		pos3.z = self.pos.z + Random[GibFx](-16, 16);
		
		if (!dontthrust)
		{
			vel3.x = self.vel.x + FRandom[GibFx](-32, 32);
			vel3.y = self.vel.y + FRandom[GibFx](-32, 32);
			vel3.z = self.vel.z + FRandom[GibFx](0, 90);
		}
		else
		{
			vel3.x = 0;
			vel3.y = 0;
			vel3.z = 0;
		}
		
		a = self.Spawn(gibtype, pos3, ALLOW_REPLACE);
		
		a.vel = vel3;
		a.A_FaceMovementDirection();
	}
	
	// Spawn Gibs. Uses a flag to spawn specific parts
	void A_SpawnGibsFx(int flags = SGFX_SPAWNALL)
	{		
		for (int i = 0; i < 23; i++)
		{
			A_SHDX_SpawnGib("SHDX_WallGib");
		}
		
		for (int i = 0; i < 28; i++)
		{
			A_SHDX_SpawnGib("SHDX_Giblets");
		}
		
		for (int i = 0; i < 4; i++)
		{
			A_SHDX_SpawnGib("SHDX_BoneGibs");
		}
		
		if (flags & SGFX_SPAWNHEAD)
		{
			A_SHDX_SpawnGib("SHDX_HeadGib");
		}
		
		if (flags & SGFX_SPAWNEYES)
		{
			A_SHDX_SpawnGib("SHDX_EyeGib");
			A_SHDX_SpawnGib("SHDX_EyeGib");
		}
		
		if (flags & SGFX_SPAWNARM)
		{
			A_SHDX_SpawnGib("SHDX_ArmGib");
		}
		
		if (flags & SGFX_SPAWNLEG)
		{
			A_SHDX_SpawnGib("SHDX_LegGib");
		}
	}
	
	// Spawn ashes
	void A_SpawnAshFx()
	{			
		for (int i = 0; i < 20; i++)
		{
			A_SHDX_SpawnGib("SHDX_AshGibs");
		}
	}
	
	// Spawn metal junk
	void A_SpawnMetalJunkFx()
	{			
		for (int i = 0; i < 32; i++)
		{
			A_SHDX_SpawnGib("SHDX_MetalJunk");
		}
	}
	
	// Spawn nuts and bolts
	void A_SpawnMetalJunk2Fx()
	{			
		for (int i = 0; i < 8; i++)
		{
			A_SHDX_SpawnGib("SHDX_MetalJunk1");
			A_SHDX_SpawnGib("SHDX_MetalJunk2");
			A_SHDX_SpawnGib("SHDX_MetalJunk3");
			A_SHDX_SpawnGib("SHDX_MetalJunk4");
		}
	}
	
	// Spawn glass
	void A_SpawnGlassJunkFx()
	{			
		for (int i = 0; i < 32; i++)
		{
			A_SHDX_SpawnGib("SHDX_GlassJunk");
		}
	}
}