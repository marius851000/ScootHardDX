/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_RocketGuardSpot : SHDX_Actor
{
	Default
	{
		//$Category "Monsters"
		//$Sprite ZMCLB0
		//$Title "Surprise Rocket Guard Spawning Spot"
		Radius 16;
		Height 56;
		
		+NOBLOCKMAP
	}
	
	States
	{
		Spawn:
			TNT1 A 4;
			Loop;
		SpawnInRocketGuard:
			TNT1 A 0
			{
				A_SpawnItemEx("SHDX_TeleportFog", flags:SXF_NOCHECKPOSITION);
				A_SpawnItemEx("SHDX_RocketGuard", flags:SXF_NOCHECKPOSITION);
			}
			Goto Spawn;
	}
}

// Eri coined this "The Simp Wall"

class SHDX_SurpriseHandler : EventHandler
{
	SHDX_SurpriseBoss surpboss;
	
	int rocketguardtics;
	
	const TIMETOSPAWNROCKETGUARDS = (TICRATE * 30);
	const TIMETOCOPYARRAY = (TICRATE * 10);
	
	override void WorldTick()
	{
		SurpriseRoutine();
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		CheckIfSpawnedSurprise(e.Thing);
	}
	
	void CheckIfSpawnedSurprise(Actor a)
	{
		let s = SHDX_SurpriseBoss(a);
		if (s)
			surpboss = s;
	}
	
	void SurpriseRoutine()
	{		
		if (surpboss)
		{
			if (surpboss.isactive)
			{
				rocketguardtics++;
				if (rocketguardtics >= TIMETOSPAWNROCKETGUARDS)
				{
					SpawnRocketGuardsFromSpots();
					rocketguardtics = 0;
				}
				if (surpboss.health <= 0)
				{
					KillRocketGuards();
					surpboss = null;
				}
			}
		}
	}
	
	void KillRocketGuards()
	{
		ThinkerIterator rgit = ThinkerIterator.Create("SHDX_RocketGuard");
		SHDX_RocketGuard rg;
		
		// iterate through the spots
		while (rg = SHDX_RocketGuard(rgit.Next()))
		{
			// if spot exists
			if (rg)
				rg.A_Die('Extreme');
		}
	}
	
	void SpawnRocketGuardsFromSpots()
	{
		ThinkerIterator rgspotit = ThinkerIterator.Create("SHDX_RocketGuardSpot");
		SHDX_RocketGuardSpot rgspot;
		
		// iterate through the spots
		while (rgspot = SHDX_RocketGuardSpot(rgspotit.Next()))
		{
			// if spot exists
			if (rgspot)
				rgspot.SetStateLabel("SpawnInRocketGuard");
		}
	}
}