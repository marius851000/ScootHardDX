/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_Sandwich : SHDX_Health
{
	Default
	{
		//$Sprite SWCHA0
		//$Title "Sandwich"
		//$Category "Health and Armor"
		
		Inventory.Amount 10;
		Tag "$SHDX_TAG_SANDWICH";
		
		Radius 10;
		Height 10;
		
		Inventory.PickupMessage "$SHDX_PICKUP_SANDWICH";
	}
	
	States
	{
		Spawn:
			SWCH A -1;
			Stop;
	}
}

Class SHDX_Pizza : SHDX_Health
{
	Default
	{
		//$Sprite PZZAA0
		//$Title "Pizza"
		//$Category "Health and Armor"
		
		Inventory.Amount 25;
		Tag "$SHDX_TAG_PIZZA";
		
		Radius 10;
		Height 10;
		
		Inventory.PickupMessage "$SHDX_PICKUP_PIZZA";
	}
	
	States
	{
		Spawn:
			PZZA A -1;
			Stop;
	}
}

Class SHDX_Applesauce : SHDX_Health
{
	Default
	{
		//$Sprite SAUCA0
		//$Title "Applesauce"
		//$Category "Health and Armor"
		
		Health 10;
		
		Inventory.Amount 25;
		Tag "$SHDX_TAG_APPLESAUCE";
		Radius 12;
		Height 12;
		Inventory.PickupMessage "$SHDX_PICKUP_APPLESAUCE";
		+VULNERABLE
		+NOTAUTOAIMED
		+NOBLOOD
		+DONTTHRUST
		DamageFactor "Explosive", 2.0;
		DamageFactor "Normal", 0.0;
		DamageFactor "Bullet", 0.0;
	}
	
	bool heatedup;
	
	// If heated up, we need a different applesauce pickup message
	override String PickupMessage()
	{
		if (heatedup)
			return StringTable.Localize("$SHDX_PICKUP_APPLESAUCEHOT");
		
		return Super.PickupMessage();
	}
	
	
	States
	{
		Spawn:
			SAUC A -1;
			Stop;
		
		Death.Explosive:
			SAUC B 0
			{
				// Heated up!
				A_StartSound("misc/foodcook", CHAN_BODY);
				
				Amount = 50;
				
				heatedup = true;
			}
			SAUC B -1 Bright;
			Stop;
	}
}

Class SHDX_Cupcake : SHDX_Health
{
	Default
	{
		//$Sprite CCAKA0
		//$Title "Cupcake"
		//$Category "Health and Armor"
		
		Inventory.Amount 100;
		Inventory.MaxAmount 200;
		Inventory.PickupMessage "$SHDX_PICKUP_CUPCAKE";
		Tag "$SHDX_TAG_CUPCAKE";
	}
	
	// Give a cringe message
	override bool TryPickup(in out Actor toucher)
	{
		if (toucher.health < MaxAmount)
		{
			String cupcakejokeloc = String.Format("%s%02d", "$SHDX_CUPCAKE_", Random[Cupcake](1, 34));
			String str = String.Format("%s\n%s", StringTable.Localize("$SHDX_CUPCAKE_SAYS"), Stringtable.Localize(cupcakejokeloc));
			toucher.A_Print(str, 15, 'smallfont');
		}
		
		return Super.TryPickup(toucher);
	}
	
	States
	{
		Spawn:
			CCAK A -1;
			Stop;
	}
}