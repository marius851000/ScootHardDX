/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_SpectraAmmo : Ammo
{
	Default
	{
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 5;
	}
}

Class SHDX_Rainbower : SHDX_BFGWeapon
{
	Default
	{
		//$Title "Rainbower"
		//$Sprite GUN6A0
		Tag "$SHDX_TAG_RAINBNOWER";
		Weapon.SelectionOrder 10;
		Inventory.PickupMessage "$SHDX_PICKUP_RAINBOWER";
		Weapon.AmmoType1 "SHDX_SpectraAmmo";
		Weapon.AmmoType2 "SHDX_SpectraAmmo";
		Weapon.AmmoGive 5;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 1;
		+Weapon.NoAutoFire
		+Weapon.NoAutoAim
		Obituary "$SHDX_OB_RAINBOWER";
	}
	
	const ANGLE_INCREMENT_RBFXB = 2.8125;
	const ANGLE_MINMAX = 67.5;
	
	States
	{
		Ready:
			RBWG A 1 A_WeaponReady;
			Loop;
		Deselect:
			RBWG A 1 A_Lower(10);
			Loop;
		Select:
			RBWG A 1 A_Raise(8);
			Loop;
		Flash:
			TNT1 A 8 A_Light1;
			goto LightDone;
		Flash2:
			TNT1 A 6 A_Light1;
			goto LightDone;
		Fire:
			RBWG C 6
			{
				A_GunFlash();
				A_StartSound("weapons/rainbowerf", CHAN_WEAPON);
				A_FireProjectile("SHDX_RainbowerProjectileA");
			}
			RBWG A 1;
			RBWG A 0 A_ReFire;
			Goto Ready;
		AltFire:
			RBWG A 6
			{
				A_GunFlash();
				A_StartSound("weapons/rainbowerf2", CHAN_WEAPON);
				
				for (float i = -ANGLE_MINMAX; i < ANGLE_MINMAX; i += ANGLE_INCREMENT_RBFXB)
				{
					A_FireProjectile("SHDX_RainbowerProjectileB", i, false);
				}
				
				A_TakeInventory("SHDX_SpectraAmmo", 1);
			}
			RBWG B 2;
			RBWG A 0 A_ReFire;
			Goto Ready;
		Spawn:
			GUN6 A -1;
			Stop;
	}
}

const SHDX_RBOWER_FLAGS = (CMF_AIMDIRECTION|CMF_TRACKOWNER);

Class SHDX_RainbowerProjectileA : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 6;
		Speed 30;
		Damage 60;
		Mass 5000;
		Projectile;
		+Corpse;
		DamageType "Spectrum";
		DeathSound "weapons/rainbowerx";
	}
	
	const OFFSET_AMT = 32;
	
	int counter;
	int offset;
	
	States
	{
		Spawn:
			RBWN AA 2;
			RBWN A 0 A_Gravity;
			Loop;
		
		Death:
		XDeath:
		Crash:
			TNT1 A 1
			{
				A_StartSound("weapons/rainbower2");
				A_SpawnProjectile("SHDX_RainbowerFxA", 0, flags:SHDX_RBOWER_FLAGS);
				counter = 1;
			}
		DeathLoop:
			TNT1 A 2
			{
				offset = OFFSET_AMT * counter;
				
				A_SpawnProjectile("SHDX_RainbowerFxA", 0, offset, flags:SHDX_RBOWER_FLAGS);
				A_SpawnProjectile("SHDX_RainbowerFxA", 0, -offset, flags:SHDX_RBOWER_FLAGS);
				
				counter++;
				
				if (counter >= 6)
					return ResolveState("Null");
				
				return ResolveState(null);
			}
			Loop;
	}
}

Class SHDX_RainbowerFxA : SHDX_Actor
{
	Default
	{
		+MISSILE
		+DROPOFF
		+FOILINVUL
		+RANDOMIZE
		+RIPPER
		+NOEXPLODEFLOOR
		+STEPMISSILE
		MaxStepHeight 16;
		Damage 50;
		Speed 35;
		DamageType "Spectrum";
		RenderStyle "Add";
		Alpha 0.7;
		DeathSound "weapons/rainbowerx";
	}
	
	States
	{
		Spawn:
			RBOW A 5 Bright;
			Loop;
		Death:
			RBWN BCD 4 Bright;
			Stop;
	}
}



Class SHDX_RainbowerProjectileB : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 45;
		Damage 35;
		RenderStyle "Translucent";
		Alpha 1.0;
		Projectile;
		+RANDOMIZE
		+RIPPER
		DamageType "Spectrum";
		DeathSound "weapons/rainbowerx";
	}
	
	States
	{
		Spawn:
			RBWN A 5 Bright A_FadeTo(0.0, 0.142, FTF_REMOVE|FTF_CLAMP);
			Loop;
		
		Death:
			RBWN B 3 Bright
			{
				alpha = 1.0;
			}
			RBWN CD 3 Bright;
			Stop;
	}
}