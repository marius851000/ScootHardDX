/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_LightningGunAmmo : Ammo
{
	Default
	{
		Inventory.MaxAmount 75;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 75;
	}
}

Class SHDX_LightningGun : SHDX_WeatherWeapon
{
	Default
	{
		//$Title "Lightning Gun"
		//$Sprite LGUNA0
		
		Tag "$SHDX_TAG_LIGHTNINGGUN";
		Weapon.SelectionOrder 500;
		Inventory.PickupMessage "$SHDX_PICKUP_LIGHTNINGGUN";
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOAUTOAIM
		+WEAPON.CHEATNOTWEAPON
		Weapon.AmmoGive 75;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType1 "SHDX_LightningGunAmmo";
		Weapon.AmmoType2 "SHDX_LightningGunAmmo";
		Obituary "$SHDX_OB_LIGHTNINGGUN";
	}
	
	action void A_FireLightningPlasma()
	{
		A_StartSound("weapons/lightningf", CHAN_WEAPON, CHANF_OVERLAP);
		A_FireProjectile("SHDX_LightningGunPlasmaBall");
	}
	
	action void A_FireLightningAltFire()
	{
		A_StartSound("weapons/lightningf", CHAN_WEAPON, CHANF_OVERLAP);
		A_FireProjectile("SHDX_LightningGunDisintegratingBall");
		A_TakeInventory("SHDX_LightningGunAmmo", 24);
	}
	
	States
	{
		Spawn: 
			LGUN A -1;
			LOOP;
		Ready: 
			LTGG A 1 A_WeaponReady;
			LOOP;
		Deselect: 
			LTGG A 1 A_Lower(10);
			LOOP;
		Select:
			LTGG A 1 A_Raise(10);
			Loop;
		Flash:
			LTGF ABCDABCD 2 Bright A_Light1;
			goto LightDone;
		AltFlash:
			LTGF EF 2 Bright A_Light1;
			goto LightDone;
		AltFire.LowAmmo:
		Fire:
			LTGG A 2
			{
				A_GunFlash("Flash");
				A_FireLightningPlasma();
			}
			LTGG B 2 A_WeaponOffset(8, 41, WOF_INTERPOLATE);
			LTGG C 2
			{
				A_FireLightningPlasma();
				A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			}
			LTGG D 2 A_WeaponOffset(8, 41, WOF_INTERPOLATE);
			LTGG E 2
			{
				A_FireLightningPlasma();
				A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			}
			LTGG A 2 A_WeaponOffset(8, 41, WOF_INTERPOLATE);
			LTGG B 2
			{
				A_FireLightningPlasma();
				A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			}
			LTGG C 2 A_WeaponOffset(8, 41, WOF_INTERPOLATE);
			LTGG D 2
			{
				A_FireLightningPlasma();
				A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			}
			LTGG E 2 A_WeaponOffset(8, 41, WOF_INTERPOLATE);
			LTGG E 2;
			LTGG A 2 A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			LTGG A 0 A_ReFire;
			Goto Ready;
		AltFire:
			LTGG A 0
			{
				if (CountInv("SHDX_LightningGunAmmo") >= 25)
					return ResolveState("AltFire.HasAmmo");
					
				return ResolveState("AltFire.LowAmmo");
			}
		AltFire.HasAmmo:
			LTGG F 2
			{
				A_GunFlash("AltFlash");
				A_FireLightningAltFire();
				A_WeaponOffset(24, 44, WOF_INTERPOLATE);
			}
			LTGG G 2 A_WeaponOffset(36, 48, WOF_INTERPOLATE);
			LTGG H 2 A_WeaponOffset(24, 44, WOF_INTERPOLATE);
			LTGG I 2 A_WeaponOffset(12, 42, WOF_INTERPOLATE);
			LTGG J 1 A_WeaponOffset(1, 40, WOF_INTERPOLATE);
			LTGG A 0 A_ReFire;
			Goto Ready;
	}
}

Class SHDX_LightningGunDisintegratingBall : SHDX_Projectile
{
	Default
	{
		Radius 18;
		Height 18;
		Speed 20;
		Damage 8;
		Scale 0.24;
		Projectile;
		+RANDOMIZE
		DamageType "Lightning";
		// SeeSound "weapons/lightningf";
		DeathSound "weapons/lightningx";
		
		Translation "112:127=84:90";
		
		+RIPPER
	}
	
	void A_DestroyNearbyProjectiles()
	{
		Actor a;
		ThinkerIterator ti = ThinkerIterator.Create("SHDX_Projectile");
		double blockdist;
		while (a = Actor(ti.Next()))
		{
			if (!a)
				continue;
			
			if (a == self)
				continue;
			
			if (a.target == self.target)
				continue;
			
			blockdist = radius + a.radius;
							
			if (abs(pos.x - a.pos.x) > blockdist || abs(pos.y - a.pos.y) > blockdist)
				continue;
			
			if (abs(a.pos.z - pos.z) > blockdist)
				continue;
			
			a.Destroy();
		}
	}
	
	override int SpecialMissileHit(Actor victim)
    {
        let zomb = SHDX_Zombie(victim);
		if (zomb)
		{
			zomb.overchargecounter = 4;
			zomb.A_Die('Lightning');
		}
		
       return Super.SpecialMissileHit(victim);
    }
	
	States
	{
		Spawn:
			PLAS AB 1 Bright NoDelay
			{
				A_DestroyNearbyProjectiles();
				A_SpawnParticle("00FF00", SPF_FULLBRIGHT|SPF_RELATIVE, TICRATE, 4, startalphaf:0.6);
				for (int i = 0; i < 6; i++)
				{
					A_SpawnParticle("00FF00", SPF_FULLBRIGHT|SPF_RELATIVE, TICRATE, 4, xoff:FRandom[LightningGun](-16.0, 16.0), yoff:FRandom[LightningGun](-16.0, 16.0), startalphaf:0.6);
				}
			}
			Loop;
		Death:
			PLAS C 2 Bright;
			PLAS DE 2;
			Stop;
	}
}

Class SHDX_LightningGunPlasmaBall : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 24;
		Damage 6;
		Projectile;
		+RANDOMIZE
		DamageType "Lightning";
		// SeeSound "weapons/lightningf";
		DeathSound "weapons/lightningx";
	}
	
	States
	{
		Spawn:
			PLAS AB 1 Bright;
			Loop;
		Death:
			PLAS C 2 Bright;
			PLAS DE 2;
			Stop;
	}
}