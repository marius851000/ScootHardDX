/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_MissileAmmo : Ammo
{
	Default
	{
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 10;
	}
}

Class SHDX_MissileLauncher : SHDX_MissileWeapon
{
	Default
	{
		//$Title "Missile Launcher"
		//$Sprite GUN4A0
		
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 10;
		Weapon.AmmoType1 "SHDX_MissileAmmo";
		Weapon.AmmoType2 "SHDX_MissileAmmo";
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOAUTOAIM
		Inventory.PickupMessage "$SHDX_PICKUP_MISSILELAUNCHER";
		Tag "$SHDX_TAG_MISSILELAUNCHER";
		Obituary "$SHDX_OB_MISSILELAUNCHER";
	}
	
	States
	{
		Ready:
			MISG A 1 A_WeaponReady;
			Loop;
		Deselect:
			MISG A 1 A_Lower(10);
			Loop;
		Select:
			MISG A 1 A_Raise(10);
			Loop;
		Flash:
			MISF A 3 Bright A_Light1;
			goto LightDone;
		AltFlash:
			MISF A 3 Bright A_Light1;
			goto LightDone;
		Fire:
			MISG A 3 A_GunFlash;
			MISG B 2;
			MISG B 4
			{
				A_StartSound("weapons/rocketf", CHAN_WEAPON);
				A_FireProjectile("SHDX_Missile");
			}
			MISG B 1; // ACS_NamedExecute("SHDX_OutOfAmmo", 0, 1)
			MISG B 0 A_ReFire;
			Goto Ready;
		AltFire:
			MISG A 3 A_GunFlash;
			MISG B 2;
			MISG B 2
			{
				A_StartSound("weapons/grenadef", CHAN_WEAPON);
				A_FireProjectile("SHDX_Grenade2");
			}
			MISG B 1; // ACS_NamedExecute("SHDX_OutOfAmmo", 0, 1)
			MISG B 0 A_ReFire;
			Goto Ready;
		Spawn:
			GUN4 A -1;
			Stop;
	}
}

Class SHDX_Missile : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 45;
		Damage 20;
		Projectile;
		+RANDOMIZE
		DamageType "Explosive";
		// SeeSound "weapons/rocketf";
		DeathSound "weapons/rocketx";
		Obituary "$SHDX_OB_MISSILE";
		
		+ROCKETTRAIL
	}
	
	States
	{
		Spawn:
			MISL A 1 Bright;
			Loop;
		
		Death:
			EXPL A 0 Bright
			{
				A_Explode(64, 128, damagetype:'Explosive');
				// SHDX_Utilities.DoExplosion(self, 64, 32, 128, DamageType:'Explosive');
			}
			EXPL A 3 Bright A_Quake(2, 8, 0, 1096);
			EXPL BCDEFGHI 3 Bright;
			Stop;
	}
}

Class SHDX_Grenade2 : SHDX_Grenade
{
	Default
	{
		Radius 7;
		Height 14;
		Speed 25;
		Damage 20;
		DamageType "Explosive";
		
		Scale 0.14;
		Mass 25;
		
		BounceCount 5;
		BounceType "Hexen";
        BounceFactor 0.65;
        WallBounceFactor 0.7;
		
		BounceSound "enforcer/grenadeb";
		Deathsound "enforcer/grenadex";
		
		Gravity 0.75;
		
		+CanBounceWater
	}
	
	override int SpecialMissileHit(Actor victim)
	{
		if (victim && victim != target)
		{
			let hbx = SHDX_Hitbox(victim);
			if (hbx && hbx.iscorpse)
			{
				return 1;
			}
			
			if (victim is "SHDX_Enemy")
			{
				return -1;
			}
		}
		
		return Super.SpecialMissileHit(victim);
	}

	States
	{
		Spawn:
			GREN ABCD 1 Bright;
			GREN D 0 Bright
			{
				bNoGravity = false;
			}
			GREN ABCD 1 Bright;
			Loop;
		
		Death:
			EXPL A 3 Bright
			{
				A_SetRenderStyle(0.9, STYLE_Add);
				A_Explode(64, 176, damagetype:'Explosive');
				// SHDX_Utilities.DoExplosion(self, 64, 128., 176, flags:DE_EXTRAZTHRUST);
				A_Stop();
			}
			EXPL B 3 Bright
			{
				A_Quake(2, 8, 0, 1096);
				A_Stop();
			}
			EXPL CDEFGHI 3 Bright
			{
				A_FadeOut(0.1);
				A_Stop();
			}
			Stop;
	}
}